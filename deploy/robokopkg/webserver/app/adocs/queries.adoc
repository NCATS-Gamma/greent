== One Hop Queries

++++
<p>
The simplest queries are those that go a single step from a named entity.  These one hop queries can be useful in exploring the data.
</p>
<p>
Find all genes associated with Ebola Hemmorhagic Fever:
++++
[source,cypher]
----
match (a:disease {id:"MONDO:0005737"})-[x]-(g:gene) return *
----
++++
</p>

<p>
Each relation has a predicate, and for many queries you may be interested in a subset of these predicates.  A query like this can show you what predicates exist for a particular node. Find the relations that chemicals have to the gene named INS (insulin):
++++
[source,cypher]
----
match (a:chemical_substance)-[x]->(b:gene {name:"INS"}) return distinct type(x), count(*) as c order by c desc
----
++++
</p>

<p>
Find the chemicals that increase the response to insulin:
++++
[source,cypher]
----
match (a:chemical_substance)-[x:increases_response_to]->(g:gene {name:"INS"}) return *
----
++++
</p>

<p>
Find diseases that have a basis in the regulation of membrane potential (GO:0042391):
++++
[source,cypher]
----
match (d:disease)-[:disease_has_basis_in]->(b:biological_process_or_activity {id:"GO:0042391"}) return *
----
++++
</p>
++++

//------------------------------------------- slide - 2 -----------------------------------------------

== Multiple Hop Queries

++++
<p>
More complicated queries can be created by stringing together multiple hops. 
</p>

<p>
Find all genes that are part of a gene family with the FTO gene (HGNC:24687)
++++
[source,cypher]
----
match (g:gene {id:"HGNC:24678"})--(f:gene_family)--(g2:gene) return *
----
++++
</p>
<p>
Find all genetic conditions that share a gene with Ebola:
++++
[source,cypher]
----
match (d:disease {id:"MONDO:0005737"})--(g:gene)--(c:genetic_condition) return *
----
++++
</p>
<p>
Find all processes that occur in cells that are associated with the liver:
++++
[source,cypher]
----
match (b:anatomical_entity {name:"liver"})--(c:cell)--(d:biological_process_or_activity) return *
----
++++
</p>
<p>
Find all cells that are part of the brain, and are capable of thyroid stimulating hormone secretion.  Note in this case that we are specifying both ends of the path as input.
++++
[source,cypher]
----
match (a:anatomical_entity {name:"brain"})--(c:cell)--(b:biological_process_or_activity {id:"GO:0070460"}) return *
----
++++
</p>

<p>
Find any concepts that connect brain to diabetes.  In this case, the connection are brain-affecting phenotypes that occur in diabetes.
++++
[source,cypher]
----
match p=(b:anatomical_entity {name:"brain"})--()--(d:disease {id:"MONDO:0005148"}) return *
----
++++
</p>
++++


== NonLinear Queries

++++
<p>
All of the queries so far find linear paths.  But cypher allows more complex query patterns.
</p>
<p>
Find chemicals that treat leukemia (MONDO:0005059) and interact with a gene that is involved with leukemia.
++++
[source,cypher]
----
match p=(a:disease {id:"MONDO:0005059"})<-[e1:treats]-(c:chemical_substance)-[e2]-(g:gene)-[e3]-(a) return * limit 10
----
++++
</p>
<p>
Find all genes connected to LEP through both a shared biological process, and a shared chemical:
++++
[source,cypher]
----
match (g1:gene {id:"HGNC:6553"})-[e1]-(b:biological_process_or_activity)-[e2]-(g:gene)-[e3]-(c:chemical_substance)-[e4]-(g1) return * limit 20
----
++++
</p>
<p>
Find all genes, biological processes, and chemicals that are connected to Hyperglycemia, and to one another.
++++
[source,cypher]
----
match (c:chemical_substance)-[e1]-(d:disease {id:"MONDO:0002909"})-[e2]-(b:biological_process_or_activity)-[e3]-(g:gene)-[e4]-(c)-[e5]-(b),(g)-[e6]-(d) return *
----
++++
</p>
++++
<p>
Given a HGNC gene identifier, find the sequence variants that have some effect on the gene's expression on a human tissue.
++++
[source,cypher]
----
match (g:gene)-[e1:decreases_expression_of]-(s:sequence_variant)-[e2]-(a:anatomical_entity)--(g) where g.id='HGNC:23093' and any (q in e1.hyper_edge_id where any (w in e2.hyper_edge_id where w=q)) return *
----
++++
</p>
++++

== Jaccard Similarities

++++
<p>
Robokop KG can be used to perform more complicated calculations in queries, such as Jaccard similarity coefficients.
</p>
<p>
Find diseases that have a jaccard coefficient of greater than 0.2 with obesity, when measured on shared genes.
++++
[source,cypher]
----
MATCH (query:disease {id:"MONDO:0011122"})--(b:gene)--(result:disease) 
WITH query, result, count(distinct b) as intersection, collect(distinct b.id) as i
MATCH (query)--(qm:gene)
WITH query,result, intersection,i, COLLECT(distinct qm.id) AS s1
MATCH (result)--(rm:gene)
WITH query,result,intersection,i, s1, COLLECT(distinct rm.id) AS s2
WITH query,result,intersection,s1+filter(x IN s2 WHERE NOT x IN s1) AS union, s1, s2
WITH query,result,intersection,union,s1,s2, ((1.0*intersection)/SIZE(union)) AS jaccard
WHERE jaccard > 0.2
RETURN result, jaccard ORDER BY jaccard DESC
----
++++
</p>
++++

== Drug Repurposing Queries

++++
<p>
These queries can be used as the starting point for investigations of drug repurposing.
</p>
<p>
Find drugs based on a mechanistic understanding of a gene's role:   If a chemical is known to contribute to a condition, and if that chemical increases the activity of a gene that is also known to be involved with the condition, then find other chemicals that decrease activity of that gene and perhaps they will treat the condition.   Here, we limit the new chemicals to those that are already drugs, and we also add a biological process node connecting the intial toxic chemical and the gene:
++++
[source,cypher]
----
MATCH (d:disease {id:"MONDO:0005052"})<-[:contributes_to]-(c:chemical_substance)-[:increases_activity_of]->(g:gene)--(d),
(c)--(b:biological_process_or_activity)--(g)<-[:decreases_activity_of]-(c2:chemical_substance {drug:true}) 
RETURN *
----
++++
</p>
<p>
Given a rare disease, find a related common disease that share a gene and biological process with the rare disease.  Find drugs that treat the common disease and are related to the gene.
++++
[source,cypher]
----
MATCH (d:disease {id:"MONDO:0018305"})--(g:gene)--(d2:disease)<-[:treats]-(c:chemical_substance)--(g),
(d)--(b:biological_process_or_activity)--(d2),
(g)--(b) 
WHERE d.rare_disease=true 
AND NOT EXISTS(d2.rare_disease) 
AND c.drug=true 
RETURN *
----
++++
</p>
++++

